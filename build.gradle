import java.nio.charset.StandardCharsets

plugins {
    id "org.sonarqube" version "2.6"
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "jacoco"
    apply plugin: "project-report"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url "http://repo1.maven.org/maven2/" }
    }

    test {
        useJUnit()

        jacoco {
            enabled = true

            reports {
                html.enabled = true
            }
        }

        outputs.upToDateWhen {
            false
        }

        testLogging {
            showStandardStreams = false
        }
    }

}

project.ext {
    url = "https://github.com/dkorobtsov/LoggingInterceptor"
    version = "5.0-SNAPSHOT"
}

def _name = "Pretty Logging Interceptor: Core"
def _description = "Core library for Logging Interceptors"
def _encoding = StandardCharsets.UTF_8.name()
def _mavenLocal = this.hasProperty("maven.repo.local")
        ? this.property("maven.repo.local") : "./build/repo"

def allTestCoverageFile = "$buildDir/jacoco/allTestCoverage.exec"

group = "com.dkorobtsov.logging"
archivesBaseName = "LoggingInterceptor"
version = "5.0-SNAPSHOT"


jar {
    manifest {
        attributes(
                "Implementation-Title": _name,
                "Automatic-Module-Name": project.group + "." + project.name,
                "Implementation-Version": project.version,
                "Implementation-URL": project.url
        )
    }
    from sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
    source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

artifacts {
    archives javadocJar, sourcesJar
}

subprojects {
    sonarqube {
        properties {
            property "sonar.jacoco.reportPaths", allTestCoverageFile
        }
    }
}

if (this.hasProperty("remote")) {
    apply plugin: "signing"

    signing {
        sign configurations.archives
    }
}

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = this.properties["ossrhUsername"]
def _ossrhPassword = this.properties["ossrhPassword"]


uploadArchives {
    repositories {
        mavenDeployer {

            //use: gradle uploadArchives -Premote
            if (this.hasProperty("remote")) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }
            } else {
                repository(url: uri(_mavenLocal))
            }

            pom.project {
                name _name
                packaging "jar"
                artifactId archivesBaseName
                description _description
                url project.url
                inceptionYear "2018"

                scm {
                    url project.url
                    connection "scm:" + project.url + ".git"
                    developerConnection "scm:" + project.url + ".git"
                }


                licenses {
                    license {
                        name "MIT"
                        url "https://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "dkorobtsov"
                        name "Dmitri Korobtsov"
                        email "dmitri.korobtsov@gmail.com"
                    }
                }

                issueManagement {
                    system "GitHub issues"
                    url project.url + "/issues"
                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = _encoding
    options.debug = true
}

task jacocoMergeTest(type: JacocoMerge) {
    destinationFile = file(allTestCoverageFile)
    executionData = project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
}

task jacocoMerge(dependsOn: ['jacocoMergeTest']) {
    // used to run the other merge tasks
}

task cleanAll(type: Delete) {
    delete "out", "apache-interceptor/out"
    delete "out", "okhttp-interceptor/out"
    delete "out", "okhttp3-interceptor/out"
    delete "out", "interceptor-core/out"
    delete "out", "interceptor-tests/out"
    followSymlinks = true
}

task getDependencies(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into "runtime/"
}

